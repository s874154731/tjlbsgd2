ext {
    // 测试服
    def SERVER_TYPE_TEST = "test"
    // 正式服
    def SERVER_TYPE_PRODUCT = "product"

    def taskName = project.gradle.startParameter.taskNames[0]
    if (taskName == null) {
        taskName = ""
    }
    // 打印当前执行的任务名称
    println "GradleLog TaskNameOutput " + taskName

    def serverType = SERVER_TYPE_PRODUCT

    if (taskName.endsWith("Debug")) {
        serverType = SERVER_TYPE_TEST
    }

    // 从 Gradle 命令中读取参数配置，例如：./gradlew assembleRelease -P ServerType="test"
    if (project.hasProperty("ServerType")) {
        serverType = project.properties["ServerType"]
    }

    // 打印当前服务器配置
    println "GradleLog ServerTypeOutput " + serverType

    // 友盟 AppKey
    UMENG_APP_KEY = ""
    // QQ AppId
    QQ_APP_ID = ""
    // QQ Secret
    QQ_APP_SECRET = ""
    // 微信 AppId
    WX_APP_ID = ""
    // 微信 Secret
    WX_APP_SECRET = ""

    switch(serverType) {
        case SERVER_TYPE_TEST:
            LOG_ENABLE = true
            HOST_URL = "http://82.157.115.45:8886/"
            break
        case SERVER_TYPE_PRODUCT:
            LOG_ENABLE = false
            HOST_URL = "http://82.157.115.45:8886/"
            break
    }
}